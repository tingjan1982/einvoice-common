plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'maven-publish'
}

group 'io.nextpos.einvoice'
version '1.5-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

/**
 * https://docs.gradle.org/current/userguide/declaring_dependencies.html
 */
configurations {
    extraLibs
    implementation.extendsFrom extraLibs
}

dependencies {

    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

    implementation 'jakarta.persistence:jakarta.persistence-api'
    implementation 'javax.xml.bind:jaxb-api'

    implementation 'org.apache.commons:commons-lang3:3.11'
    compileOnly 'org.projectlombok:lombok'

    // encryption specific dependencies
    extraLibs files('lib/geinv-kms-core-1.0.4.jar')
    extraLibs files('lib/geinv-kms-dist-1.0.1.jar')
    extraLibs files('lib/tv-commons-1.0.4.jar')
    //extraLibs files('lib/tv-logging-core-1.0.3.jar')
    //extraLibs files('lib/tv-config-1.0.2.jar')
    extraLibs files('lib/tv-process-monitor-1.0.0.jar')
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-configuration:commons-configuration:1.10'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:3.0.0'
    testImplementation 'org.awaitility:awaitility:3.0.0'
}

// create einvoice-common as library rather than application
bootJar {
    enabled = false
}

jar {
    enabled = true
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    // https://discuss.gradle.org/t/how-to-include-dependencies-in-jar/19571
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

/**
 * Gradle maven publish plugin:
 * https://docs.gradle.org/6.3/userguide/publishing_maven.html#publishing_maven:resolved_dependencies
 * GitHub Packages:
 * https://docs.github.com/en/packages/using-github-packages-with-your-projects-ecosystem/configuring-gradle-for-use-with-github-packages
 */
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'io.nextpos.einvoice'
            artifactId = 'einvoice-common'
            version = '1.5.2'
            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name 'GitHubPackages'
            url 'https://maven.pkg.github.com/tingjan1982/einvoice-common'
            credentials {
                username project.findProperty("gpr.username") as String ?: System.getenv("GPR_USERNAME")
                password project.findProperty("gpr.password") as String ?: System.getenv("GPR_PASSWORD")
            }
        }
    }
}